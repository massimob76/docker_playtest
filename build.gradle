
apply plugin: 'java'
apply plugin: 'jetty'

jettyRun.contextPath = '';

repositories {
    jcenter()
}

configurations {
    itestCompile.extendsFrom testCompile
    itestRuntime.extendsFrom testRuntime
}

sourceSets {
    itest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output

        java.srcDir file('src/itest/java')
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.itest.output.classesDir
    classpath = sourceSets.itest.runtimeClasspath

    doFirst {
        jettyRun.httpPort = 8090
        jettyRun.daemon = true
        jettyRun.stopPort = 8091
        jettyRun.stopKey = 'stopKey'
        jettyRun.execute()
    }
    doLast {
        jettyStop.stopPort = 8091
        jettyStop.stopKey = 'stopKey'
        jettyStop.execute()
    }
}

check.dependsOn integrationTest

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'org.glassfish.jersey.core:jersey-server:2.22.2'
    compile 'org.glassfish.jersey.containers:jersey-container-jetty-servlet:2.22.2'

    testCompile 'junit:junit:4.12'
    testCompile 'org.glassfish.jersey.core:jersey-client:2.22.2'
}
